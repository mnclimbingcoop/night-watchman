import org.gradle.api.publish.maven.MavenPublication

buildscript {
    ext {
        springBootVersion = '1.2.3.RELEASE'
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
    }
}

apply plugin: "spring-boot"
apply plugin: "application"
apply plugin: "maven-publish"
apply plugin: "com.jfrog.bintray"

mainClassName="com.mnclimbingcoop.app.DoorWatchmanApp"

dependencies {

    compile project(':hid-api')

    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "com.squareup.okhttp:okhttp-urlconnection:${okhttpVersion}"
    compile "com.squareup.okhttp:okhttp:${okhttpVersion}"

    compile "com.squareup.retrofit:converter-jackson:${retrofitVersion}"
    compile("com.squareup.retrofit:retrofit:${retrofitVersion}") {
        exclude group: 'com.google.guava', module: 'guava'
    }

    compile 'javax.annotation:javax.annotation-api:1.2'
    compile 'javax.inject:javax.inject:1'

    testCompile "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    description "An archive of the source code for Maven Central"
    classifier "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    description "An archive of the JavaDocs for Maven Central"
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publish.dependsOn assemble

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {

    user = project.hasProperty('bintrayUsername') ? project.getProperty('bintrayUsername') : ''
    key = project.hasProperty('bintrayApiKey') ? project.getProperty('bintrayApiKey') : ''

    publications = [ 'mavenJava' ]

    dryRun = false
    publish = true

    pkg {
        repo = 'maven'
        name = 'night-watchman'
        userOrg = 'mnclimbingcoop'
        desc = 'An app to gather and publish information about HID EdgePro / EVO units.'
        websiteUrl = 'https://github.com/mnclimbingcoop/night-watchman'
        issueTrackerUrl = 'https://github.com/mnclimbingcoop/night-watchman/issues'
        vcsUrl = 'https://github.com/mnclimbingcoop/night-watchman.git'
        licenses = [ 'Apache-2.0' ]
        labels = [ 'hid', 'edge', 'evo' ]
        publicDownloadNumbers = true
        version {
            name = "${project.version}"
            vcsTag = "${version}"
        }
    }
}
