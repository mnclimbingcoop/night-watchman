import org.gradle.api.publish.maven.MavenPublication

apply plugin: "spring-boot"
apply plugin: "application"
apply plugin: "maven-publish"
apply plugin: "com.jfrog.bintray"

run {
    // support passing system properties via command line to override -Dspring.profiles.active=deployed
    jvmArgs = ["-Dspring.profiles.active=${System.properties['spring.profiles.active'] ?: 'dev'}"]
}

dependencies {
    compile project(':hid-api')
    compile project(':shared-spring')

    testCompile "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    description "An archive of the source code for Maven Central"
    classifier "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    description "An archive of the JavaDocs for Maven Central"
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publish.dependsOn assemble

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {

    user = project.hasProperty('bintrayUsername') ? project.getProperty('bintrayUsername') : ''
    key = project.hasProperty('bintrayApiKey') ? project.getProperty('bintrayApiKey') : ''

    publications = [ 'mavenJava' ]

    dryRun = false
    publish = true

    pkg {
        repo = 'maven'
        name = "${project.name}"
        userOrg = 'mnclimbingcoop'
        desc = 'An app to gather and publish information about HID EdgePro / EVO units.'
        websiteUrl = 'https://github.com/mnclimbingcoop/night-watchman'
        issueTrackerUrl = 'https://github.com/mnclimbingcoop/night-watchman/issues'
        vcsUrl = 'https://github.com/mnclimbingcoop/night-watchman.git'
        licenses = [ 'Apache-2.0' ]
        labels = [ 'hid', 'edge', 'evo' ]
        publicDownloadNumbers = true
        version {
            name = "${project.version}"
            vcsTag = "${version}"
        }
    }
}
